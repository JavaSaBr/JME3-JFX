buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

plugins {
  id 'org.openjfx.javafxplugin' version '0.0.8'
}

javafx {
    version = "13"
    modules = [ 'javafx.controls' ]
}

apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group = 'com.jme3'
version = '3.0.0'

compileJava {
    options.encoding = "UTF-8"
    options.incremental = true
}

sourceCompatibility = 1.11
targetCompatibility = 1.11

ext.rlibVersion = "9.9.0"
ext.bintrayVersion = version

def jme3 = [v: '3.2.4-stable', g: 'org.jmonkeyengine']

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven {
        url  "https://dl.bintray.com/javasabr/maven"
    }
}

publishing {
    publications {
        LibArtifact(MavenPublication) {
            from components.java
            groupId group
            artifactId 'jfx'
            version bintrayVersion
            afterEvaluate {
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}

bintray {
    user = System.getProperty('bintrayUser')
    key = System.getProperty('bintrayKey')
    publications = ['LibArtifact']
    pkg {
        repo = 'maven'
        name = 'com.jme3.jfx'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/JavaSaBr/JME3-JFX'
        vcsUrl = 'https://github.com/JavaSaBr/JME3-JFX.git'
        issueTrackerUrl = 'https://github.com/JavaSaBr/JME3-JFX/issues'
        githubRepo = 'JavaSaBr/JME3-JFX'
        githubReleaseNotesFile = 'README.md'
        version {
            name = bintrayVersion
            desc = bintrayVersion
            released  = new Date()
        }
    }
}

dependencies {
    implementation  group: 'org.jetbrains', name: 'annotations', version: '18.0.0'
    implementation  group: 'com.spaceshift', name: 'rlib.common', version: rlibVersion
    implementation  "${jme3.g}:jme3-core:${jme3.v}"
    implementation  "${jme3.g}:jme3-lwjgl3:${jme3.v}"
}

javadoc {
    failOnError = false
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

wrapper {
    gradleVersion = '6.1.1'
}